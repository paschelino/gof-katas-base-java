apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'jetty'

sourceCompatibility = 1.7
version = '0.0.1'
jar {
    manifest {
        attributes 'Implementation-Title': 'gof-katas-base-java', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

sourceSets {
	acceptancetest {
		java {
			srcDir 'src/acceptancetest/java'
		}
		resources {
			srcDir 'src/acceptancetest/resources'
		}
		compileClasspath += sourceSets.main.runtimeClasspath
	}
}

task acceptancetest() {
	dependsOn assemble, compileTestJava, compileAcceptancetestJava
	doFirst {
		println 'Starting the embedded jetty server'
		println 'chrome webdriver is found at: ' + System.properties['webdriver.chrome.driver']
		jettyRun.httpPort = 8080    // Port for test
		jettyRun.daemon = true
		jettyRun.execute()
	}
	doLast {
		javaexec {
			main = "cucumber.api.cli.Main"
			classpath = configurations.acceptancetestRuntime + sourceSets.main.output + sourceSets.acceptancetest.output
			args = ['-f', 'pretty', '--glue', 'gradle.cucumber', 'src/acceptancetest/resources']
			jvmArgs= ['-Dwebdriver.chrome.driver='+System.properties['webdriver.chrome.driver']]
		}
		jettyStop.stopPort = 8091   // Port for stop signal
		jettyStop.stopKey = 'stopKey'
		jettyStop.execute()
	}
}

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
	
	// web apis
	providedCompile 'javax:javaee-api:7.0'

	// unit tests
    testCompile 'junit:junit:4.11'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	
	// integration / acceptance tests
    acceptancetestCompile 'junit:junit:4.11'
	acceptancetestCompile 'org.hamcrest:hamcrest-all:1.3'
	acceptancetestCompile 'info.cukes:cucumber-java:1.1.2'
	acceptancetestCompile 'info.cukes:cucumber-junit:1.1.2'
	acceptancetestCompile 'info.cukes:cucumber-picocontainer:1.1.2'
	acceptancetestCompile files("${jar.archivePath}")
	acceptancetestCompile 'org.seleniumhq.selenium.fluent:fluent-selenium:1.9'
            
}

test {
    systemProperties 'webdriver.chrome.driver': '/gof-katas-base-java/src/test/resources/info/cosmicsand/gofkatas/base/webdriver/chrome/chromedriver'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}


